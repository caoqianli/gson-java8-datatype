buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'

group = 'net.dongliu'
version = '1.0.2'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}
dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Database Utils'
                packaging 'jar'
                description 'Java8 data type module for gson'
                url 'https://github.com/caoqianli/gson-java8-datatype'

                scm {
                    connection 'scm:git:https://github.com/caoqianli/gson-java8-datatype'
                    developerConnection 'scm:git:https://github.com/caoqianli/gson-java8-datatype'
                    url 'https://github.com/caoqianli/gson-java8-datatype'
                }

                licenses {
                    license {
                        name 'The BSD2-Clause License'
                        url 'https://github.com/caoqianli/gson-java8-datatype/blob/master/LICENSE.txt'
                    }
                }

                developers {
                    developer {
                        id 'caoqianli'
                        name 'Liu Dong'
                        email 'dongliu@live.cn'
                    }
                }
            }
        }
    }
}

task release {
    dependsOn uploadArchives
    doLast {
        closeAndPromoteRepository.execute()
    }
}